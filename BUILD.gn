# Copyright (C) 2018-2023 The ANGLE Project Authors.
# Copyright (C) 2019-2023 LunarG, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

is_ohos = current_os == "ohos"
is_android = current_os == "android"
is_mac = current_os == "ios" || current_os == "tvos" || current_os == "mac"
is_win = current_os == "win" || current_os == "mingw"
is_linux = current_os == "linux"
is_fuchsia = current_os == "fuchsia"

config("vulkan_headers_config") {
  include_dirs = [ "include" ]
  defines = []

  if (is_ohos) {
    defines += [ "VK_USE_PLATFORM_OHOS" ]
  }

  if (is_win) {
    defines += [ "VK_USE_PLATFORM_WIN32_KHR" ]
  }
  if (is_linux) {
    if (defined(vulkan_use_x11) && vulkan_use_x11) {
      defines += [ "VK_USE_PLATFORM_XCB_KHR" ]
    }
  }
  if (defined(vulkan_use_wayland) && vulkan_use_wayland) {
    defines += [ "VK_USE_PLATFORM_WAYLAND_KHR" ]
    if (defined(vulkan_wayland_include_dirs)) {
      include_dirs += vulkan_wayland_include_dirs
    }
  }
  if (is_android) {
    defines += [ "VK_USE_PLATFORM_ANDROID_KHR" ]
  }
  if (is_fuchsia) {
    defines += [ "VK_USE_PLATFORM_FUCHSIA" ]
  }
  if (is_mac) {
    defines += [ "VK_USE_PLATFORM_METAL_EXT" ]
  }
  if (defined(is_ggp) && is_ggp) {
    defines += [ "VK_USE_PLATFORM_GGP" ]
  }
  if (is_clang) {
    cflags = [ "-Wno-redundant-parens" ]
  }
}

# Vulkan headers only, no compiled sources.
ohos_static_library("vulkan_headers") {
  sources = [
    "include/vk_video/vulkan_video_codec_h264std.h",
    "include/vk_video/vulkan_video_codec_h264std_decode.h",
    "include/vk_video/vulkan_video_codec_h264std_encode.h",
    "include/vk_video/vulkan_video_codec_h265std.h",
    "include/vk_video/vulkan_video_codec_h265std_decode.h",
    "include/vk_video/vulkan_video_codec_h265std_encode.h",
    "include/vk_video/vulkan_video_codecs_common.h",
    "include/vulkan/vk_icd.h",
    "include/vulkan/vk_layer.h",
    "include/vulkan/vk_platform.h",
    "include/vulkan/vulkan.h",
    "include/vulkan/vulkan.hpp",
    "include/vulkan/vulkan_core.h",
    "include/vulkan/vulkan_ohos.h",
  ]
  public_configs = [ ":vulkan_headers_config" ]

  license_file = "//third_party/vulkan-headers/LICENSE.txt"
}
